{
    parserClass="com.github.andreypfau.intellijton.tlb.parser.TlbParser"
    extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

    psiClassPrefix="Tlb"
    psiImplClassSuffix="Impl"
    psiPackage="com.github.andreypfau.intellijton.tlb.psi"
    psiImplPackage="com.github.andreypfau.intellijton.tlb.psi.impl"
    implements="com.github.andreypfau.intellijton.tlb.psi.TlbElement"

    elementTypeHolderClass="com.github.andreypfau.intellijton.tlb.psi.TlbTypes"
    elementTypeClass="com.github.andreypfau.intellijton.tlb.psi.TlbElementType"
    tokenTypeClass="com.github.andreypfau.intellijton.tlb.psi.TlbTokenType"

    tokens = [
        HEX_TAG='regexp:#([0-9a-f]|_)'

        PLUS='+'
        MINUS='-'
        TIMES='*'
        COLUMN=':'
        SEMICOLUMN=';'
        LPAREN='('
        RPAREN=')'
        LBRACE='{'
        RBRACE='}'
        LBRACKET='['
        RBRACKET=']'
        EQUALS='='
        UNDERSCORE='_'
        QUESTION='?'
        DOT='.'
        TILDE='~'
        DOUBLE_TAG='##'
        TAG='#'
        CIRCUMFLEX='^'
        DOLLAR='$'
        EQ='=='
        LESS='<'
        GREATER='>'
        LEQ='<='
        GEQ='>='
        NEQ='!='
        TYPE='Type'
        EMPTY='EMPTY'
        ANY='Any'
        CELL='Cell'
        INT='int'
        UINT='uint'
        BITS='bits'
        NAT_LESS='#<'
        NAT_LEQ='#<='
        UINT1 = 'uint1'
        UINT2 = 'uint2'
        UINT4 = 'uint4'
        UINT8 = 'uint8'
        UINT16 = 'uint16'
        UINT32 = 'uint32'
        UINT64 = 'uint64'
        UINT128 = 'uint128'
        UINT256 = 'uint256'
        UINT257 = 'uint257'
        BITS1 = 'bits1'
        BITS2 = 'bits2'
        BITS4 = 'bits4'
        BITS8 = 'bits8'
        BITS16 = 'bits16'
        BITS32 = 'bits32'
        BITS64 = 'bits64'
        BITS128 = 'bits128'
        BITS256 = 'bits256'
        BITS512 = 'bits512'
        BITS1023 = 'bits1023'
        WHITE_SPACE='regexp:\s+'
        COMMENT='regexp:(//.*)'
        BINARY_TAG='regexp:\$[01]*_?'
        NUMBER='regexp:[0-9]+'
        IDENTIFIER='regexp:[a-zA-Z_][0-9a-zA-Z0-9_]*'
    ]
}

root ::= combinator_declaration*

combinator_declaration ::= constructor field_definition_list '=' combinator ';' {pin=1}

constructor ::= ( '_' | constructor_name )
constructor_name ::= IDENTIFIER [ constructor_prefix ]
constructor_prefix ::= HEX_TAG | BINARY_TAG
combinator ::= combinator_name type_expression* {pin=1}
combinator_name ::= IDENTIFIER

private field_definition_list_recovery ::= !( '=' | ']' )
field_definition_list ::= field_definition* {recoverWhile=field_definition_list_recovery}
field_definition ::= implicit_definition | named_field | paren_expression
implicit_definition ::= '{' ( implicit_field | expression ) '}' {pin=1}
named_field ::= ( '_' | IDENTIFIER ) ':' expr95 {pin=1}
implicit_field ::= IDENTIFIER ':' ( '#' | 'Type' )

anonymous_constructor ::= '[' field_definition_list ']' {pin=1}

cell_ref ::= '^' type_expression {pin=1}

expression ::= expr10
private expr10 ::= expr20 '=' expr20 | expr20 '<=' expr20 | expr20 '<' expr20 | expr20 '>=' expr20 | expr20 '>' expr20 | expr20
private expr20 ::= expr30 ( '+' expr30 )*
private expr30 ::= expr90 ( '*' expr90 )*
private expr90 ::= expr95+
private expr95 ::= expr97 [ cond_type ]
private cond_type ::= '?' type_expression {pin=1}
private expr97 ::= type_expression [ '.' type_expression ]

type_expression ::= [ '~' ] ( paren_expression | anonymous_constructor | cell_ref | builtin_type | NUMBER | IDENTIFIER )
paren_expression ::= "(" expression ')' {pin=1}
builtin_type ::= '#<' | '#<=' | '##' | '#' | 'Any' | 'Cell' | 'int' | 'Type' | uint | bits
private uint ::= 'uint' | 'uint1' | 'uint2' | 'uint4' | 'uint8' | 'uint16' | 'uint32' | 'uint64' | 'uint128' | 'uint256' | 'uint257'
private bits ::= 'bits' | 'bits1' | 'bits2' | 'bits4' | 'bits8' | 'bits16' | 'bits32' | 'bits64' | 'bits128' | 'bits256' | 'bits512' | 'bits1023'

//noinspection BnfUnusedRule
private unused_in_bnf ::= LINE_COMMENT | BLOCK_COMMENT | LINE_DOCUMENTATION | BLOCK_DOCUMENTATION